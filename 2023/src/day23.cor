let N = -1i, S = 1i, E = 1, W = -1
let ADJ = [N, S, E, W]
let SLOPES = {N: '^', S: 'v', E: '>', W: '<'}

let inp = read_text './inputs/day23.txt' . split '\n'

let height = len(inp)
  , width = len(inp[0])

// Make an observation that the easiest way to avoid bounds checks, is to move the start and end points one step inwards, and close off the
// outer edges of the graph. We can then adjust all path lengths by +2 in the end

inp[0] = '#' * width
inp[-1] = '#' * width

// First, compute all nodes on the graph
// We make an observation that the input is largely linear, with few actual nodes that present decisions
// So, this is best formulated as a graph problem (longest simple path), which is simple in part 1, and NP-hard in part 2 (due to the presence of cycles)
let start = 1 + 1i
  , end = width - 2 + height * 1i - 2i
let nodes = {start, end}
for y, line in inp . enumerate {
    for x, c in line . enumerate {
        if c == '.' and (line[x - 1] != '#') + (line[x + 1] != '#') + (inp[y - 1][x] != '#') + (inp[y + 1][x] != '#') > 2 {
            nodes.push(x + y * 1i)
        }
    }
}

// In order to construct the edges, we run a BFS from each node, stopping at each adjacent node reached
let edges = dict() . default list

fn bfs(start) {
    let queue = [(start, 0, false)]
    let seen = set()
    while queue {
        let pos, cost, slope = queue.pop

        if pos in nodes and pos != start {
            edges[start].push((pos, cost, slope))
            continue
        }

        if pos in seen {
            continue
        }

        seen.push(pos)

        for adj in ADJ {
            let next = pos + adj
            let c = inp[imag next][real next]
            if c == '.' or c == SLOPES[adj] {
                queue.push((next, cost + 1, slope))
            } elif c != '#' {
                queue.push((next, cost + 1, true)) // This path requires traversing *up* a slope - legal on part 2, not on part 1
            }
        }
    }
}

for node in nodes {
    bfs node
}

// Here, re-label the nodes on the graph to simple integral values, 0 = start, 1 = end, etc.
// Also split the edge list into two seperate edge sets, one for part 1 (without moving up slopes),
// and one for part 2 (all edges)
nodes .= list

// Use flat lists here for speed
let part1 = range(len nodes) . map(fn(_) -> [])
  , part2 = range(len nodes) . map(fn(_) -> [])

for k, vs in edges {
    k = nodes . index_of k
    for v, cost, slope in vs {
        v = nodes . index_of v
        part2[k].push((v, cost))
        if !slope {
            part1[k].push((v, cost))
        }
    }
}


fn longest_path(edges) {
    // The best cost, incremented whenever we reach a sink position

    let best = 0

    fn run(node, cost, remaining_cost, seen) {
        // If, counting all possible remaining cost, we cannot beat the best possible cost, we early exit
        // This is the key optimization that reduces the runtime from ~30s -> ~1s
        if cost + remaining_cost <= best {
            return
        }
        
        // If we reach the target, update the best cost, and return
        if node == 1 {
            best max= cost
            return
        }

        // For all edges that fanout from this node, remove them as potential remaining_cost,
        // if we have not already seen that destination node.
        // This is a very accurate way of keeping track of how much additional cost we might be
        // able to reach, for any given selection of an edge.
        for dest, dest_cost in edges[node] {
            if seen & (1 << dest) == 0 {
                remaining_cost -= dest_cost
            }
        }

        // Then, check all edges, using our new value of `remaining_cost` as a heuristic, to avoid
        // exploring paths that are worse than the currently known best cost.
        for dest, dest_cost in edges[node] {
            let mask = 1 << dest
            if seen & mask == 0 {
                run(dest, cost + dest_cost, remaining_cost, seen | mask)
            }
        }
    }

    // Total cost is the sum of the weight of all edges. Since this graph is not directed,
    // we sum all the fanout weights, and divide by two.
    let total_cost = edges
        . flat_map(map([1]))
        . sum

    // Run the DFS
    run(0, 0, total_cost / 2, 1)

    // The result is the best cost we found, +2 for the hack we did at the beginning w.r.t initial positions
    best + 2
}

print('Part 1:', longest_path part1)
print('Part 2:', longest_path part2)
// Day 01: 141th / 158th

let inp = read_text './inputs/day01.txt' . split '\n'
let digits = ['one', 'two', 'three', 'four', 'five', 'six', 'seven', 'eight', 'nine']

fn solve(part2) {
    let total = 0
    for line in inp {
        let min_x = -1,
            max_x = -1
        
        for i, c in line . enumerate {
            // Checking if a digit is numeric, we can do be seeing if it's parsable as an int
            // Since '0' is not valid, the only false value here is `nil`
            if int(c, nil) {
                if min_x == -1 {
                    min_x = int c
                }
                max_x = int c
            }
            
            // For part 2, we do the same thing, but check slices against each digit word
            // Slices do not error as we slice off the end, and will just not compare equal
            if part2 {
                for j, n in digits . enumerate {
                    if line[i:i + len(n)] == n {
                        if min_x == -1 {
                            min_x = j + 1
                        }
                        max_x = j + 1
                    }
                }
            }
        }

        assert min_x != -1 and max_x != -1
        total += max_x + 10 * min_x
    }
    total
}

print('Part 1:', solve(false))
print('Part 2:', solve(true))
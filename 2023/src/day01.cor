// Day 01: 141th / 158th

let inp = read_text './inputs/day01.txt' . split '\n'
let digits = ['one', 'two', 'three', 'four', 'five', 'six', 'seven', 'eight', 'nine']

fn solve(part2) {
    let total = 0
    for line in inp {
        let min_x = -1,
            max_x = -1
        
        for i in line . len . range {
            if line[i] in '123456789' {
                if min_x == -1 {
                    min_x = int(line[i])
                }
                max_x = int(line[i])
            }
            if part2 {
                for j, n in digits . enumerate {
                    if line[i:i + len(n)] == n {
                        if min_x == -1 {
                            min_x = j + 1
                        }
                        max_x = j + 1
                    }
                }
            }
        }

        assert min_x != -1 and max_x != -1
        total += max_x + 10 * min_x
    }
    total
}

print('Part 1:', solve(false))
print('Part 2:', solve(true))